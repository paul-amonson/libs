// Copyright (C) 2018 Paul Amonson
//
// SPDX-License-Identifier: Apache-2.0
//

def honorCoverageRules = true

def gitVersionString = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

println("*** Building version: " + gitVersionString() + "\n")

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = gitVersionString()
    sourceCompatibility = 11
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    clean {
        delete += "${projectDir}/out"
    }

    build {
        finalizedBy(javadoc)
    }

    javadoc {
//        options.addBooleanOption('html5', true)
        doLast {
            copy {
                from(javadoc.outputs)
                into "${rootProject.buildDir}/docs/${project.name}"
            }
            copy {
                from(file("${rootProject.projectDir}/index.html"))
                from(file("${rootProject.projectDir}/stylesheet.css"))
                into "${rootProject.buildDir}/docs"
            }
        }
    }

    compileJava {
        options.compilerArgs << '-Werror'
        options.compilerArgs << '-Xlint:all,-processing,-path'
        options.compilerArgs << '-XDignore.symbol.file=true'
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    task copyJars(type: Copy) {
        from jar.outputs
        from configurations.compile
        into "${rootProject.buildDir}/libs"
    }
    jar.finalizedBy(copyJars)

    test {
        reports {
            html.destination file("${rootProject.buildDir}/reports/test/${project.name}")
        }
        finalizedBy(jacocoTestReport)
    }

    jacocoTestReport {
        group = "Reporting"
        reports {
            xml.enabled = true
            csv.enabled = false
            html.destination file("${rootProject.buildDir}/reports/coverage/${project.name}")
        }
        finalizedBy(jacocoTestCoverageVerification)
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'PACKAGE'
                enabled = honorCoverageRules
                limit {
                    counter = 'METHOD'
                    minimum = 0.90
                }
            }
            rule {
                element = 'PACKAGE'
                enabled = honorCoverageRules
                limit {
                    counter = 'BRANCH'
                    minimum = 0.70
                }
            }
        }
    }
}

project(':crypto') {
    group = 'com.amonson'
    dependencies {
        compile project(':prop_store')
    }
}

project(':prop_store') {
    group = 'com.amonson'
    dependencies {
        compile group: 'com.github.cliftonlabs', name: 'json-simple', version: '3.0.2+'
        compile group: 'org.yaml', name: 'snakeyaml', version: '1.21+'
    }
}

project(':logger') {
    group = 'com.amonson'
    dependencies {
    }
}

task cleanUp(type: Delete) {
    delete += "${rootProject.buildDir}/libs/${rootProject.name}-${rootProject.version}.jar"
}
jar.finalizedBy(cleanUp)
